<html>
<head>
<title>zx spectrum source listings archive - randomize usr</TITLE>
</head>

<body>
<pre>
archive-name: <a href="">listings.rand-usr.com</a>/<a href="">basic</a>/<a href="">books</a>/<a href="">aprende_a_programar</a>/<a href="">los_invasores.bas</a>
related files: <a href="">z80 snapshot</a> - <a href="">tap file</a>
last-modified: jan 28, 2026

----------------------------------- SOURCE ------------------------------------

source original (not found)
title: "write your own program" creating a database
genre: book
publisher: aladdin books ltd.
author: steve rodgers & marcus milton
year: 1985
isbn: unknown

source (spanish edition)
title: aprende a programar: el banco de datos - aventuras
genre: book
publisher: e.g. anaya
author: pedro garre del olmo (trans.)
year: 1985
isbn: 84-7525-316-4

found at: page 42
listing: 2

----------------------------------- NOTES -------------------------------------



----------------------------------- START -------------------------------------

5 REM despegue del cohete
10 CLS
20 PRINT AT 4,10;"DESPEGUE"
30 PRINT AT 5,10;"--------"
40 PRINT AT 8,4;"EL COHETE ESTA PREPARADO"
50 PRINT 10,4;"DEBES HACERLO DESPEGAR"
60 PRINT 13,1; INK 2; FLASH 1;"solo tienes 10 intentos"
70 PRINT 16,3;"POR FAVOR, TECLEA TU NOMBRE"
80 PRINT 18,2;"Y PULSA ENTER"
90 INPUT n$
100 CLS
110 PRINT "BIENVENIDO A BORDO": PRINT "CAPITAN ";n$
120 PRINT AT 4,1;"PARA HACER DESPEGAR EL COHETE"
130 PRINT AT 6,1;"DEBES ADIVINAR EL NIVEL DE"
140 PRINT AT 8,1;"ENERGIA NECESARIO"
150 PRINT AT 10,1;"(UN NUMERO ENTRE 1 Y 100)"
160 PRINT AT 12,1;"TECLEA EL NUMERO Y PULSA ENTER"
170 LET nivel=INT (RND*100)+1
180 FOR i=1 TO 10
185 PRINT AT 16,8; INK 2; FLASH 1;"INTENTO NUMERO ";i
190 INPUT num
195 PRINT AT 17,16;num;"   "
200 IF num=nivel THEN GOTO 280
210 IF num<nivel THEN PRINT AT 20,1;"DEMASIADO BAJO, PRUEBA OTRA VEZ"
220 IF num>nivel THEN PRINT AT 20,1;"DEMASIADO ALTO, PRUEBA OTRA VEZ"
230 NEXT i
240 CLS
250 PRINT AT 4,0;"HAS FALLADO TUS 10 INTENTOS"
260 PRINT AT 6,0;"TECLEA 'RUN' (ENTER) PARA JUGAR";AT 7,0;"OTRA VEZ"
270 STOP
280 CLS
290 LET altitud=0
300 LET objetivo=10000
310 PRINT AT 2,10;"ELEVACION !!!!!"
320 PRINT AT 10,10;"ALTITUD"
330 PRINT AT 10,20;altitud
340 FOR r=1 TO 100
350 NEXT r
360 LET altitud=altitud*2+1
365 BEEP .5,30
370 IF altitud<objetivo THEN GOTO 330
380 CLS
390 FOR r=5 TO 10
400 PRINT AT r,8;"COHETE EN ORBITA !"
410 NEXT r
420 PRINT AT 15,1;"ENHORABUENA CAPITAN ";n$
430 LET f$="ASTEROIDES"
440 GO SUB 3000
1000 REM asteroides
1010 CLS
1020 DIM c(50): DIM f(50)
1030 FOR s=1 TO 50
1040 LET c(s)=INT (RND*26)+3
1050 LET f(s)=INT (RND*18)+2
1055 LET xxx=INT (RND*7)
1060 PRINT AT f(s),c(s); INK xxx;CHR$ 132
1065 PRINT AT f(s)+1,c(s); INK xxx;CHR$ 135;CHR$ 130
1070 NEXT s
1100 LET col=0: LET fila=2+INT (RND*18)
1110 PRINT AT fila,col;"X"
1160 INPUT "DIRECCION (norte,sur,este) ";d$
1170 IF d$="n" THEN LET arriba=-1: LET derecha=0: GOTO 1210
1180 IF d$="s" THEN LET arriba=1: LET derecha=0: GOTO 1210
1190 IF d$="e" THEN LET arriba=0: LET derecha=1: GOTO 1210
1200 GO TO 1160
1210 INPUT "DISTANCIA ";distancia
1220 PRINT AT fila,col;"O"
1230 FOR c=1 TO distancia
1240 LET fila=fila+arriba
1250 LET col=col+derecha
1260 FOR s=1 TO 50
1270 IF col=c(s) AND fila=f(s) THEN GOTO 1370
1280 IF col=c(s) AND fila=f(s)+1 THEN GOTO 1370
1285 IF col=c(s)+1 AND fila=f(s)+1 THEN GOTO 1370
1290 NEXT s
1300 IF fila>20 OR fila<2 THEN GOTO 1390
1310 PRINT AT fila,col;"O"
1320 IF col>30 THEN GOTO 1410
1330 NEXT c
1340 GO TO 1110
1370 CLS: PRINT AT 10,0;"HAS CHOCADO CON UN ASTEROIDE"
1380 PRINT AT 13,0;"TU NAVE ESTA DESTROZADA": STOP
1390 CLS: PRINT AT 10,0;"TE HAS SALIDO DE ORBITA"
1400 PRINT AT 13,0;"Y TE PIERDES EN EL ESPACIO": STOP
1410 CLS: PRINT AT 10,0;"LOS HAS ESQUIVADO BIEN !!"
1420 PRINT AT 20,0;"ERES UN BUEN PILOTO ESPACIAL"
1430 LET f$="ATERRIZAJE"
1440 GO SUB 3000
2000 REM aterrizaje del cohete
2010 CLS
2020 LET d=18000: LET v=400: LET a=2000: LET f=5
2030 LET c$="     "
2040 PRINT AT 1,0; INK 2;"ALTURA "; INK 4;"CAIDA "; INK 3;"VELOCIDAD "; INK 1;"DISTANCIA"
2050 PRINT AT 21,28; INK 6;CHR$ 143;CHR$ 143;CHR$ 143
2060 LET x=0: LET y=3
2070 PRINT AT y,x; INK 1;CHR$ 142;CHR$ 140
2080 PRINT AT 2,1;c$
2090 PRINT AT 2,1; INK 2;a
2100 PRINT AT 2,9;c$
2110 PRINT AT 2,9; INK 4;f
2120 PRINT AT 2,15;c$
2130 PRINT AT 2,15; INK 3;v
2140 PRINT AT 2,25;c$
2150 PRINT AT 2,25; INK 1;d
2160 IF v<200 THEN PRINT AT 10,5;"EL MOTOR SE TE HA PARADO": STOP
2170 IF d<-999 THEN PRINT AT 10,0;"EL COHETE SE HA PASADO": STOP
2180 IF a<=0 THEN GOTO 2290
2190 LET r$=INKEY$
2200 IF r$="z" THEN LET f=f+1: LET v=v+5
2210 IF r$="m" THEN LET f=f-1: LET v=v-5
2220 IF f=0 THEN LET f=1: LET v=v-20
2230 LET a=a-f: LET v=v+(f-5)*3: LET d=d-INT (v/10)
2240 PRINT AT y,x;c$
2250 LET x=27-INT (d/(18000/28))
2260 LET y=20-INT (a/(2000/18))
2270 PRINT AT y,x; INK 1;CHR$ 142;CHR$ 140
2280 GOTO 2080
2290 IF d>1000 THEN PRINT AT 10,5;"ATERRIZAJE DEFECTUOSO": STOP
2300 IF v>300 THEN PRINT AT 10,5;"DEMASIADA VELOCIDAD !!": STOP
2310 IF 4>5 THEN PRINT AT 10,5;"TREN DE ATERRIZAJE ROTO": STOP
2320 PRINT AT 10,0;"ATERRIZAJE PERFECTO !!!"
2330 STOP
3000 REM
3010 FOR r=1 TO 1000
3020 NEXT r
3050 CLS
3060 FOR l=1 TO 20
3065 BEEP .25,40
3070 PRINT AT 1,2; INK 2; FLASH 1;"ALERTA ! CAMPO DE ";f$
3080 NEXT l
3090 FOR r=1 TO 30
3095 BEEP .25,40
3100 NEXT r
3110 RETURN
</pre>

</body>
</html>