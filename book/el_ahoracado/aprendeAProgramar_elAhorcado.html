<html>
<head>
<title>zx spectrum source listings archive - randomize usr</TITLE>
</head>

<body>
<pre>
archive-name: <a href="">listings.rand-usr.com</a>/<a href="">basic</a>/<a href="">books</a>/<a href="">aprende_a_programar</a>/<a href="">elBancoDeDatos_1.bas</a>
related files: <a href="">z80 snapshot</a> - <a href="">tap file</a>
last-modified: jan 28, 2026

----------------------------------- SOURCE ------------------------------------

source original (not found)
title: "write your own program" creating a database
genre: book
publisher: aladdin books ltd.
author: steve rodgers & marcus milton
year: 1985
isbn: unknown

source (spanish edition)
title: aprende a programar: el banco de datos - aventuras
genre: book
publisher: e.g. anaya
author: pedro garre del olmo (trans.)
year: 1985
isbn: 84-7525-316-4

found at: page 39
listing: 2

----------------------------------- NOTES -------------------------------------

substitute any " AB" character for its corresponding user UDG key at lines: 120,130,140,150

----------------------------------- START -------------------------------------

10 CLS
20 GOSUB 1000: REM selecciona palabra
30 GOSUB 2000: REM titulo
40 IF ganas OR pierdes=1 THEN GOTO 80
50 GOSUB 3000: REM elige letra
60 IF correcto=0 THEN GOSUB 7000: REM hombre ahorcado
70 GOTO 40
80 IF ganas=1 THEN GOSUB 4000: REM ganar
90 IF pierdes=1 THEN GOSUB 5000: REM perder
100 GOSUB 6000: REM otra partida ?
110 STOP
1000 REM selecciona palabra
1010 LET palabra=8
1020 RESTORE
1030 LET elige=INT (RND*palabra)+1
1040 FOR l=1 TO elige
1050 READ p$
1060 NEXT l
1070 LET longitud=LEN p$: LET d$=p$
1080 RETURN
2000 REM titulo
2010 CLS
2020 FOR l=5 TO 10: GOSUB 7000+1*100: NEXT l
2030 PAPER 7: INK 0: PRINT AT 4,2;"H O M B R E  A H O R C A D O"
2040 PRINT AT 21,5;"pulsa ENTER para empezar": INPUT a$
2045 CLS
2050 PRINT AT 2,2;"Letras escogidas"
2060 PRINT AT 3,2;"----------------"
2070 PRINT AT 12,2;"La palabra"
2080 PRINT AT 14,2;"del ordenador es"
2090 INK 2: PRINT AT 17,2;
2100 FOR l=1 TO longitud: PRINT "*";: NEXT l
2110 INK 0: PRINT AT 20,2;"Por favor, escoge"
2120 PRINT AT 21,6;"una letra"
2130 LET ganas=0: LET pierdes=0: LET vida=0: LET puntos=0
2140 RETURN
3000 REM elige letra
3010 INPUT c$
3015 BEEP .1,20
3020 PAPER 7: INK 0: PRINT AT 5,vida;c$
3030 IF c$>CHR$ 96 THEN LET c$=CHR$ (code c$-32)
3040 LET correcto=0
3050 FOR p=1 TO longitud
3060 IF c$<>p$(p) THEN GOTO 3110
3070 LET correcto=1: LET puntos=puntos+1
3080 LET p$=p$(1 TO p-1)+"?"+p$(p+1 TO longitud)
3090 INK 2: PRINT AT 17,p+1;c$
3110 NEXT p
3120 IF puntos=longitud THEN LET ganas=1
3130 RETURN
4000 REM ganar
4010 CLS
4020 FOR l=5 TO 10: GOSUB 7000+l*100: NEXT l
4030 INK 0: PAPER 7: PRINT AT 1,4;"Bien hecho ! tu ejecucion"
4040 PRINT AT 2,4;"queda aplazada"
4045 PLOT 222,110: DRAW -3,3
4046 PLOT 232,110: DRAW 3,3
4047 FOR s=0 TO 40: BEEP .05,s: NEXT s
4050 RETURN
5000 REM perder
5010 PAPER 7: INK 0: CLS
5020 FOR l=1 TO 10: GOSUB 7000+l*100: NEXT l
5030 INK 2: PAPER 7: PRINT AT 7,2;"Inutil !"
5040 PRINT AT 14,2;"Has perdido !"
5050 PRINT AT 14,2;"La palabra era ";: PRINT AT 16,5;d$
5051 FOR s=40 TO 0 STEP -1: BEEP .05,s: NEXT s
5052 PRINT AT 6,27;"- -"
5053 PLOT 222,110: DRAW -3,-3
5054 PLOT 232,110: DRAW -3,3
5055 PRINT AT 20,26;" ": PRINT AT 20,30;" ":
5056 PAPER 0: PRINT AT 21,27;" ": PRINT AT 21,29;" ": PAPER 7
5060 RETURN
6000 REM otra partida
6010 PRINT AT 20,1;"Quieres jugar"
6020 PRINT AT 21,1;"otra partida ?"
6030 INPUT a$
6040 IF a$(1)="s" OR a$(1)="S" THEN GOTO 10
6050 CLS: PAPER 7: INK 2: PRINT AT 10,10;"A D I O S"
6060 RETURN
7000 REM hombre ahorcado
7010 LET vida=vida+1: IF vida=10 THEN LET pierdes=1
7020 GOTO 7000+vida*100
7100 REM patibulo 1
7110 INK 0
7120 FOR r=0 TO 21: FOR c=19 TO 20
7130 PRINT AT r,c;CHR$ 143
7140 NEXT c: NEXT r
7150 RETURN
7200 REM patibulo 2
7210 INK 0
7220 FOR r=0 TO 1: FOR c=21 TO 30
7230 PRINT AT r,c;CHR$ 143
7240 NEXT c: NEXT r
7250 RETURN
7300 REM patibulo 3
7310 INK 0
7320 FOR r=100 TO 105
7330 PLOT 155,r: DRAW 60,60
7340 NEXT r
7400 REM cuerda
7410 INK 0: PLOT 227,135: DRAW 0,25
7420 RETURN
7500 REM cara
7510 INK 0: CIRCLE 227,119,15
7520 PRINT AT 6,27;"+ +"
7530 PLOT 227,115: DRAW 0,7
7540 PLOT 222,110: DRAW 10,0
7550 RETURN
7600 REM cuerpo
7610 PAPER 2
7620 FOR r=9 TO 13: PRINT AT r,27;"   ": NEXT r
7630 INK 7: PRINT AT 10,28;"O":PRINT AT 12,28;"O"
7640 RETURN
7700 REM brazo 1
7710 PAPER 2: INK 7
7720 FOR r=9 TO 14: PRINT AT r,26;" ": NEXT r
7730 PLOT 215,56: DRAW 0,35
7740 PAPER 7: INK 0: CIRCLE 210,50,4
7750 RETURN
7800 REM brazo 2
7810 PAPER 2: INK 7
7820 FOR r=9 TO 14: PRINT AT r,30;" ": NEXT r
7830 PLOT 240,56: DRAW 0,35
7840 PAPER 7: INK 0: CIRCLE 244,50,4
7850 RETURN
7900 REM pierna 1
7910 PAPER 6: INK 7
7920 FOR r=14 TO 19: PRINT AT r,27;CHR$ 128;CHR$ 133: NEXT r
7930 RETURN
8000 REM pierna 2
8010 PAPER 6
8020 FOR r=14 TO 19: PRINT AT r,28;"  ": NEXT r
8030 REM cinturon y zapatos
8040 INK 0
8050 FOR c=60 TO 63
8060 PLOT 216,c: DRAW 23,0
8070 NEXT c
8080 INK 7: CIRCLE 228,61,3
8090 PAPER 0: INK 7
8100 PRINT AT 20,26;"  ": PRINT AT 20,29;"  "
8110 PLOT 228,16: DRAW 0,35
8120 REM retardo
8130 FOR d=1 TO 100: NEXT d
8140 RETURN
9000 DATA "MANZANA","ANGULO","ALFABETO","ANTIGUO"
9010 DATA "ALUBIAS","LIBRO","BUFALO","PLAYA"
</pre>

</body>
</html>