<html>
<head>
<title>zx spectrum source listings archive - randomize usr</TITLE>
</head>

<body>
<pre>
archive-name: <a href="">listings.rand-usr.com</a>/<a href="">basic</a>/<a href="">books</a>/<a href="">aprende_a_programar</a>/<a href="">elBancoDeDatos_1.bas</a>
related files: <a href="">z80 snapshot</a> - <a href="">tap file</a>
last-modified: jan 28, 2026

----------------------------------- SOURCE ------------------------------------

source original (not found)
title: "write your own program" creating a database
genre: book
publisher: aladdin books ltd.
author: steve rodgers & marcus milton
year: 1985
isbn: unknown

source (spanish edition)
title: aprende a programar: el banco de datos - aventuras
genre: book
publisher: e.g. anaya
author: pedro garre del olmo (trans.)
year: 1985
isbn: 84-7525-316-4

found at: page 39
listing: 2

----------------------------------- NOTES -------------------------------------

substitute any " AB" character for its corresponding user UDG key at lines: 120,130,140,150

----------------------------------- START -------------------------------------

10 CLS
20 GOSUB 5000: REM inicializacion
30 GOSUB 1000: REM entrada
40 GOSUB 2000: REM seleccion
50 IF fg<>1 AND fp<>1 THEN GOTO 30
60 GO SUB 55900: REM fin
70 STOP
1000 REM entrada
1010 LET v$="": LET n$=""
1020 INPUT "Que hago ahora ? ";r$
1030 IF LEN (r$)<3 THEN GO TO 1020
1040 FOR i=1 TO LEN (r$)
1050 IF r$(i TO i)=" " THEN LET v$=r$( TO 3): LET n$=r$(i+1 TO ): GOSUB 1500: LET i=LEN (r$)
1060 NEXT i
1070 IF n$<>"" THEN RETURN
1080 LET r$=r$( TO 3)
1090 IF r$="NOR" OR r$="SUR" OR r$="EST" OR r$="OES" THEN LET v$="VE ": LET n$=r$: RETURN
1100 IF r$="AYU" OR r$="INV" OR r$="MIR" THEN LET n$="AUX": LET v$=r$: RETURN
1110 PRINT "NO TE ENTIENDO !"
1120 GO TO 1010
1500 FOR j=1 TO LEN (n$)
1510 IF n$(j TO j)=" " THEN LET n$=n$(j+1 TO j+3): LET j=20
1520 NEXT j
1530 IF LEN (n$)>3 THEN LET n$=n$( TO 3)
1540 RETURN
2000 REM seleccion
2010 LET v=0: LET n=0
2020 RESTORE 6530
2030 FOR i=1 TO vv
2040 READ d$: IF d$( TO 3)=v$ THEN LET v=i: LET i=vv
2050 NEXT i
2060 RESTORE 6510
2070 FOR i=1 TO nn
2080 READ d$: IF d$( TO 3)=n$ THEN LET n=i: LET i=nn
2090 NEXT i
2100 IF n=0 OR v=0 THEN PRINT "NO TE ENTIENDO !": RETURN
2110 GO TO 2900+(100*v)
3000 REM *****ir*****
3010 IF n>4 THEN PRINT "IR A DONDE ?": RETURN
3020 IF s(p,n)=0 THEN PRINT "POR AHI NO HAY SALIDA !": RETURN
3030 IF p=16 AND (n=1 OR n=4) AND fa=0 THEN LET fp=1: RETURN
3040 LET p=s(p,n): GO TO 4000
3100 REM *****coger*****
3110 IF n=6 AND p=13 THEN PRINT "ES DEMASIADO PESADO !": RETURN
3120 IF n<8 THEN PRINT "NO SEAS TONTO !": RETURN
3130 IF o(n-7)=99 AND n$<>"ESP" THEN PRINT "YA LO HAS COGIDO !": RETURN
3140 FOR i=7 TO 10
3150 IF o(i)=p AND n=14 THEN LET n=i+7: LET es=es+1: LET i=10
3160 NEXT i
3170 IF o(n-7)=p THEN PRINT "O.K.": LET o(n-7)=99: LET im=im+1: RETURN
3180 PRINT "ESO NO ESTA AQUI !": RETURN
3200 REM *****dejar*****
3210 IF n<8 THEN PRINT "NO SEAS TONTO !": RETURN
3220 FOR i=7 TO 10
3230 IF o(i)=99 AND n=14 THEN LET n=i+7: LET es=es-1: LET i=10
3240 NEXT i
3250 IF o(n-7)<>99 THEN PRINT "TU NO TIENES ESO !": RETURN
3260 PRINT "O.K.": LET o(n-7)=p: LET i(n)=i(n-1)
3270 IF p=19 AND n=13 THEN LET fg=1
3280 RETURN
3300 REM *****cortar*****
3310 IF o(1)<>99 THEN PRINT "NO TIENES NADA AFILADO !": RETURN
3320 IF n<>11 THEN PRINT "NO PUEDES CORTAR ESO !": RETURN
3330 IF p<>5 THEN PRINT "NO PUEDES HACER ESO !": RETURN
3340 IF fc=1 THEN PRINT "YA LO HAS HECHO !": RETURN
3350 PRINT "LAS ALGAS CAEN Y APARECE"'"UNA VENTANA ABIERTA"
3360 LET o(4)=5: LET s(5,2)=11: LET fc=1
3370 LET l$(5)="Estas en el fondo del mar. Hacia el sur hay una pared con una    ventana abierta.": RETURN
3400 REM *****ponerse*****
3410 IF fa=1 AND n$="ALE" THEN PRINT "YA LAS TIENES PUESTAS !": RETURN
3420 IF o(2)=99 AND n$="ALE" THEN LET fa=1: LET o(2)=0: PRINT "TE QUEDAN MUY BIEN !": RETURN
3430 IF n=14 AND es>1 THEN PRINT "HACER ESO ES DE ESTUPIDOS !": RETURN
3440 PRINT "NO PUEDES PONERTE ESO !": RETURN
3500 REM *****dar*****
3510 IF p=10 AND n=11 THEN GO TO 3550
3520 IF p=7 AND n=12 THEN GO TO 3570
3530 IF p=20 THEN PRINT "ESO NO TE SERVIRA DE NADA !": RETURN
3540 PRINT "AQUI NADIE QUIERE ESO !": RETURN
3550 IF o(4)<>99 THEN PRINT "NO TIENES ESO !": RETURN
3560 LET o(4)=0: LET o(3)=10: PRINT "DAN VUELTAS ALREDEDOR TUYO"'"Y VES ALGO EN LA ESQUINA": RETURN
3570 IF o(5)<>99 THEN PRINT "NO TIENES ESO !": RETURN
3580 LET o(5)=0: LET s(7,2)=13: PRINT "EL PEZ COGE EL HUESO"'"Y SE RETIRA A UNA ESQUINA"
3590 LET l$(7)="Estas en una caverna. En una esquina hay un tollo con un hueso.": RETURN
3600 REM *****abrir*****
3610 IF o(3)<>99 THEN PRINT "NO TIENES NINGUNA LLAVE !": RETURN
3620 IF n=5 AND (p=3 OR p=9) THEN PRINT "NO HAY NINGUNA CERRADURA !": RETURN
3630 IF p<>13 OR n<>6 THEN PRINT "NO PUEDES HACER ESO !": RETURN
3640 IF fab=1 THEN PRINT "YA ESTA ABIERTO !": RETURN
3650 LET fab=1: LET o(7)=13: PRINT "LA LLAVE LO ABRE FACILMENTE"
3660 LET l$(13)="Estas en una habitacion con un cofre abierto en medio.": RETURN
3700 REM *****usar*****
3710 IF p<>20 OR n<>14 THEN PRINT "NO PUEDES HACER ESO AQUI !": RETURN
3720 IF es<4 THEN PRINT "NO TIENES PARES SUFICIENTES !": RETURN
3730 PRINT "EL PULPO NO SE PUEDE MOVER, Y ESO NO LE HACE NINGUNA GRACIA !"
3740 FOR i=7 TO 10: LET o(i)=0: NEXT i
3750 LET es=0: LET s(20,4)=19: LET l$(20)="Estas en un pasillo con una corriente muy fuerte hacia el oeste. Hay sentado un pulpo atado y muy entfadado.": RETURN
3800 REM *****inventario*****
3810 PRINT "ESTAS LLEVANDO:-"
3820 IF i(n)=0 THEN PRINT "NADA !": RETURN
3830 FOR i=1 TO 10
3840 IF o(i)=99 THEN PRINT o$(i)
3850 NEXT i: RETURN
3900 REM *****ayuda*****
3910 PRINT '"Estos son los verbos que puedes usar:"
3920 RESTORE 6530
3930 FOR i=1 TO vv: READ d$: PRINT d$,;: NEXT i
3940 PRINT '"(Solo necesitas teclear las tresprimeras letras)": RETURN
4000 REM *****mira*****
4010 PRINT 'l$(p): IF p=22 THEN LET fp=1: RETURN
4020 FOR i=1 TO 10
4030 IF i=5 AND p=15 THEN GO TO 4050
4040 IF o(i)=p THEN PRINT "AQUI HAY ";o$(i)
4950 NEXT i
4060 PRINT "SALIDAS:";
4070 IF s(p,1)>0 THEN PRINT "NORTE  ";
4090 IF s(p,2)>0 THEN PRINT "SUR  ";
4100 IF s(p,3)>0 THEN PRINT "ESTE  ";
4110 IF s(p,4)>0 THEN PRINT "OESTE  ";
4510 PRINT : RETURN
5000 REM inicializacion
5010 CLS : PRINT AT 0,4;"LAS CAVERNAS DE NEPTUNO"
5020 PRINT '"Has encontrado el tapon magico"'"que corresponde al desague del"'"mar, y decides colocarlo antes"'"de que el agua se salga."'"Te sumerges en el oceano y"'"comienza la aventura."
5030 PRINT '"ASEGURATE DE QUE TIENES PUESTAS LAS MAYUSCULAS"''"Si necesitas ayuda teclea AYUDA"
5040 LET nn=14: LET vv=11: LET p=3: LET im=1: LET es=0: LET fg=0: LET fp=0: LET fab=0: LET fa=0: LET fc=0
5050 DIM l$(24,176):DIM s(24,4):DIM O$(10,24):DIM o(10)
5060 FOR i=1 TO 24
5070 IF i=17 OR i=18 OR i=23 OR i=24 THEN LET l$(i)=l$(12): GOTO 5090
5080 READ d$: LET l$(i)=d$
5090 NEXT i
5100 FOR i=1 TO 24: FOR j=1 TO 4: READ s(i,j): NEXT j: NEXT i
5110 FOR i=1 TO 10: READ o$(i): READ o(i): NEXT i
5120 GO TO 4000: REM mira
5500 REM fin
5510 IF p=22 THEN PRINT "DE UNA DENTELLADA, EL TIBURON "'"TE ARRANCA LA CABEZA.": GOTO 5550
5520 IF p=16 THEN PRINT "LOS PINCHOS DE LOS ERIZOS SON "'"MUY VENENOSOS !"'"SUFRES UNA MUERTE TERRIBLE."'"LA PROXIMA VEZ TOMA"'"MAS PRECAUCIONES": GO TO 5550
5530 PRINT "EL TAPON SE METE EN EL AGUJERO"'"Y EL AGUA DEJA DE SALIR."'"FELICITACIONES! "'"HAS SALVADO LOS OCEANOS"
5550 INPUT '"QUIERES JUGAR OTRA VEZ ? ";r$
5560 IF r$(1)="S" THEN RUN
5570 PRINT "ADIOS": RETURN
6000 REM *****descripciones****
6010 DATA "Estas en el fondo del mar. El camino hacia el oeste esta bloqueado por un arrecife de coral."
6020 DATA "Estas en el fondo del mar. En el sur, una pared se alza enfrente de ti."
6030 DATA "Estas enfrente de una puerta de madera que esta en la pared. No ves el picaporte."
6040 DATA "Estas en el fondo del mar. En el sur, una pared se alza enfrente de ti."
6050 DATA "Estas en el fondo del mar. En el sur hay una pared con algas marinas formando un cuadrado sobre ella."
6060 DATA "Estas en el fondo del mar. Hacia el sur hay una pared. Un precipicio bloquea el camino al este."
6070 DATA "Estas en una caverna larga y estrecha. Al fondo ves un tollo nadando en circulo."
6080 DATA "Estas en una habitacion llena de luz. Las paredes, el techo y el suelo brillan."
6090 DATA "Estas en una oscura caverna con una puerta enorme al fondo. No ves el picaporte."
6100 DATA "Estas en una habitacion llena de hambrientos caballitos de mar. Te husmean la mano amistosamente."
6110 DATA "Estas en una pequena sala cuadrada. La pared del norte tiene una ventana, a traves de la que se ve el fondo del mar."
6120 DATA "Estas en una extrana sala cuadrada. Las paredes, el suelo, y el techo son cuadrados, asi como las puertas."
6130 DATA "Estas en una diminuta habitacion que esta casi ocupada por un baul con las iniciales D.J."
6140 DATA "Estas en una habitacion muy fria. El fango te rodea y notas una pequena corriente hacia el este."
6150 DATA "Estas en un lugar realmente tetrico ! Esta todo lleno de los huesos de exploradores anteriores."
6160 DATA "Estas en una sala cuadrada. Encima de la salida sur pone NO ENTRES. Las puertas del norte y del oeste estan llenas de hormigueantes erizos de mar."
6170 DATA "Estas en una habitacion circular con una fuerte corriente que gira en remolino y se cuela por el agujero del fondo."
6180 DATA "Estas en un pasillo con una fuerte corriente hacia el oeste. El camino esta bloqueado por los brazos de un tremendo pulpo."
6190 DATA "Estas en el camarote del capitan de un barco. Sientes el flujo del agua hacia el oeste."
6200 DATA "Ves un torbellino de agua, de donde sale un gran tiburon blanco."
6210 REM *******salidas*******
6220 DATA 0,0,2,0, 0,0,3,1, 0,0,4,2, 0,0,5,3
6230 DATA 0,0,6,4, 0,0,0,5, 0,0,8,0, 0,14,0,7
6240 DATA 0,15,0,0, 0,16,0,0, 5,17,12,0, 0,18,12,11
6250 DATA 7,0,0,0, 8,0,15,0, 9,21,16,14, 10,22,17,15
6260 DATA 11,23,18,16, 12,24,18,17, 0,0,20,0, 0,0,21,0
6270 DATA 15,0,0,20, 16,0,0,0, 17,23,24,0, 18,24,24,23
6300 REM ******objetos********
6310 DATA "UN CUCHILLO",2
6320 DATA "UN PAR DE ALETAS",6
6330 DATA "UNA LLAVE",0
6340 DATA "UN GRUPO DE ALGAS",0
6350 DATA "UN HUESO MOHOSO",15
6360 DATA "EL TAPON MAGICO",99
6370 DATA "UNAS ESPOSAS AMARILLAS",0
6380 DATA "UNAS ESPOSAS VERDES",9
6390 DATA "UNAS ESPOSAS ROJAS",17
6400 DATA "UNAS ESPOSAS AZULES",11
6500 REM *******nombres********
6510 DATA "NOR", "SUR", "EST", "OES", "AUX", "BAU", "VEN", "CUC", "ALE", "LLA", "ALG", "HUE", "TAP", "ESP"
6520 REM *******verbos*******
6530 DATA "VE ", "COGE", "DEJA", "CORTA", "PONTE", "DALE", "ABRE", "USA", "INVENTARIO", "AYUDA", "MIRA"
</pre>

</body>
</html>